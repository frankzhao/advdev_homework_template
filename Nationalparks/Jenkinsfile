// Jenkinsfile for NationalParks
podTemplate(
  label: "skopeo-pod",
  cloud: "openshift",
  inheritFrom: "maven",
  containers: [
    containerTemplate(
      name: "jnlp",
      image: "docker-registry.default.svc:5000/${GUID}-jenkins/jenkins-slave-appdev",
      resourceRequestMemory: "1Gi",
      resourceLimitMemory: "1Gi"
    )
  ]
) {
  node('skopeo-pod') {
    echo "GUID: ${GUID}"
    echo "CLUSTER: ${CLUSTER}"

    // Your Pipeline Code goes here. Make sure to use the ${GUID} and ${CLUSTER} parameters where appropriate
    // You need to build the application in directory `Nationalparks`.
    // Also copy "../nexus_settings.xml" to your build directory
    // and replace 'GUID' in the file with your ${GUID} to point to >your< Nexus instance

    def projectName="Nationalparks"
    def mvnCmd = "mvn -s ./nexus_settings.xml -f ${projectName}/pom.xml"
    // def mvnCmd = "mvn"

    // Checkout Source Code
    stage('Checkout Source') {
      git url: "https://gitlab.com/frankzhao/advdev_homework_template.git"
    }

    // The following variables need to be defined at the top level
    // and not inside the scope of a stage - otherwise they would not
    // be accessible from other stages.
    // Extract version and other properties from the pom.xml
    def groupId    = getGroupIdFromPom("${projectName}/pom.xml")
    def artifactId = getArtifactIdFromPom("${projectName}/pom.xml")
    def version    = getVersionFromPom("${projectName}/pom.xml")

    // Set the tag for the development image: version + build number
    def devTag  = "${version}-${BUILD_NUMBER}"
    // Set the tag for the production image: version
    def prodTag = "${version}"

    // Using Maven build the jar file
    // Do not run tests in this step
    stage('Build jar') {
      echo "Building version ${version}"
      sh "${mvnCmd} clean package -Dmaven.test.skip=true"
    }

    // Using Maven run the unit tests
    stage('Unit Tests') {
      echo "Running Unit Tests"
      sh "${mvnCmd} test"
    }

    // Using Maven call SonarQube for Code Analysis
    stage('Code Analysis') {
      echo "Running Code Analysis"
      sh "${mvnCmd} sonar:sonar -Dsonar.host.url=http://docker-openshift-sonarqube-${GUID}-sonarqube.apps.${CLUSTER} -Dsonar.projectName=${JOB_BASE_NAME}-${devTag}"
    }

    // Publish the built war file to Nexus
    stage('Publish to Nexus') {
      echo "Publish to Nexus"
      sh "${mvnCmd} deploy -DskipTests=true -DaltDeploymentRepository=nexus::default::http://nexus3.${GUID}-nexus.svc.cluster.local:8081/repository/releases"
    }

    // Build the OpenShift Image in OpenShift and tag it.
    stage('Build and Tag OpenShift Image') {
      echo "Building OpenShift container image nationalparks:${devTag}"

      // Start Binary Build in OpenShift using the file we just published
      sh "oc start-build nationalparks --follow --from-file=./${projectName}/target/nationalparks.jar -n ${GUID}-parks-dev"

      // Tag the image using the devTag
      openshiftTag alias: 'false', destStream: 'nationalparks', destTag: devTag, destinationNamespace: "${GUID}-parks-dev", namespace: "${GUID}-parks-dev", srcStream: 'nationalparks', srcTag: 'latest', verbose: 'false'
    }

    // Deploy the built image to the Development Environment.
    stage('Deploy to Dev') {
      echo "Deploying container image to Development Project"
      // Update the Image on the Development Deployment Config
      sh "oc set image dc/nationalparks nationalparks=docker-registry.default.svc:5000/${GUID}-parks-dev/nationalparks:${devTag} -n ${GUID}-parks-dev"

      // Deploy the development application.
      openshiftDeploy depCfg: 'nationalparks', namespace: "${GUID}-parks-dev", verbose: 'false', waitTime: '', waitUnit: 'sec'
      openshiftVerifyDeployment depCfg: 'nationalparks', namespace: "${GUID}-parks-dev", replicaCount: '1', verbose: 'false', verifyReplicaCount: 'false', waitTime: '', waitUnit: 'sec'
      openshiftVerifyService namespace: "${GUID}-parks-dev", svcName: 'nationalparks', verbose: 'false'
    }

    // Run Integration Tests in the Development Environment.
    stage('Integration Tests') {
      echo "Running Integration Tests"
      sh "curl http://nationalparks.$GUID-parks-dev.svc.cluster.local:8080/ws/healthz/"
      sh "curl http://nationalparks.$GUID-parks-dev.svc.cluster.local:8080/ws/info/"
    }

    // Copy Image to Nexus Docker Registry
    stage('Copy Image to Nexus Docker Registry') {
      echo "Copy image to Nexus Docker Registry"
      sh "skopeo copy --src-tls-verify=false --dest-tls-verify=false --src-creds openshift:\$(oc whoami -t) --dest-creds admin:admin123 docker://docker-registry.default.svc.cluster.local:5000/$GUID-parks-dev/nationalparks:${devTag} docker://nexus-registry.$GUID-nexus.svc.cluster.local:5000/nationalparks:${devTag}"

      // Tag the built image with the production tag.
      openshiftTag alias: 'false', destStream: 'nationalparks', destTag: prodTag, destinationNamespace: "$GUID-parks-dev", namespace: "$GUID-parks-dev", srcStream: 'nationalparks', srcTag: devTag, verbose: 'false'
    }

    // Blue/Green Deployment into Production
    // -------------------------------------
    // Do not activate the new version yet.
    def destApp   = "nationalparks-green"
    def activeApp = ""

    stage('Blue/Green Production Deployment') {
      activeApp = sh(returnStdout: true, script: "oc get route nationalparks -n $GUID-parks-prod -o jsonpath='{ .spec.to.name }'").trim()

      if (activeApp == "nationalparks-green") {
        destApp = "nationalparks-blue"
      }

      echo "Active Application:      " + activeApp
      echo "Destination Application: " + destApp

      sh "oc tag ${GUID}-parks-dev/nationalparks:${devTag} ${GUID}-parks-prod/${destApp}:${prodTag}"
      sh "oc set image dc/${destApp} ${destApp}=docker-registry.default.svc:5000/$GUID-parks-prod/${destApp}:${prodTag} -n $GUID-parks-prod"
    }

    stage('Switch over to new Version') {
      echo "Switching Production application to ${destApp}."
      sh "oc rollout latest dc/${destApp} -n $GUID-parks-prod"
      // sh "oc rollout status dc/${destApp} --watch -n $GUID-parks-prod"
      sh "oc patch route/nationalparks -p '{\"spec\":{\"to\":{\"name\":\"${destApp}\"}}}' -n $GUID-parks-prod"

      // recreate services
      sh "oc delete svc/${activeApp} -n ${GUID}-parks-prod"
      sh "oc expose dc/${activeApp} --port=8080 -l type=standby -n ${GUID}-parks-prod"
      sh "oc delete svc/${destApp} -n ${GUID}-parks-prod"
      sh "oc expose dc/${destApp} --port=8080 -l type=parksmap-backend -n ${GUID}-parks-prod"
      
      // sh "oc rollout latest dc/${destApp} -n $GUID-parks-prod"
      sh "oc rollout status dc/${destApp} --watch -n $GUID-parks-prod"
      // sh "oc scale dc/${destApp} --replicas=1 -n $GUID-parks-prod"
      // sh "oc scale dc/${activeApp} --replicas=0 -n $GUID-parks-prod"
    }

  }
}

// Convenience Functions to read variables from the pom.xml
// Do not change anything below this line.
def getVersionFromPom(pom) {
  def matcher = readFile(pom) =~ '<version>(.+)</version>'
  matcher ? matcher[0][1] : null
}
def getGroupIdFromPom(pom) {
  def matcher = readFile(pom) =~ '<groupId>(.+)</groupId>'
  matcher ? matcher[0][1] : null
}
def getArtifactIdFromPom(pom) {
  def matcher = readFile(pom) =~ '<artifactId>(.+)</artifactId>'
  matcher ? matcher[0][1] : null
}
